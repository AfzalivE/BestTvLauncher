pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
}
dependencyResolutionManagement {
    def githubProperties = new Properties()
    def propertiesFile = file("github.properties")
    if (propertiesFile.exists()) {
        propertiesFile.withInputStream {
            stream -> githubProperties.load(stream)
        }
    } else {
        githubProperties.put("gh_user", System.getenv("READ_PACKAGES_USER"))
        githubProperties.put("gh_token", System.getenv("READ_PACKAGES_TOKEN"))
    }

    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://maven.pkg.github.com/afzalive/android-tv-provider"
            credentials {
                username = githubProperties.get("gh_user") as String
                password = githubProperties.get("gh_token") as String
            }
        }
    }
}
rootProject.name = "Best Tv Launcher"
include ':mobile'
include ':tv'
include ':macrobenchmark'
